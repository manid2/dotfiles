" This line should not be removed as it ensures that various options are
" properly set to work with the Vim-related packages available in Debian.
runtime! debian.vim

" Vim5 and later versions support syntax highlighting. Uncommenting the next
" line enables syntax highlighting by default.
if has("syntax")
	syntax on
endif

" Uncomment the following to have Vim jump to the last position when
" reopening a file
if has("autocmd")
	au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
endif

" Uncomment the following to have Vim load indentation rules and plugins
" according to the detected filetype.
if has("autocmd")
	filetype plugin indent on
endif

" Source a global configuration file if available
if filereadable("/etc/vim/vimrc.local")
	source /etc/vim/vimrc.local
endif

" --- Basic vim settings -----------------------------------------------
set showcmd          " Show (partial) command in status line.
set showmatch        " Show matching brackets.
set autoindent smartindent  " Smart autoindent on new lines.
set hlsearch         " Highlight searches
set nospell          " Disable built-in spell check use plugin

if &diff
	" NOTE: toggling syntax here is not working as it is being overwritten
	" For xterm256 colors refer:
	" https://www.ditig.com/256-colors-cheat-sheet
	hi DiffAdd    ctermfg=White ctermbg=Black
	hi DiffDelete ctermfg=White ctermbg=DarkRed
	hi DiffChange ctermfg=White ctermbg=DarkGrey
	hi DiffText   ctermfg=White ctermbg=DarkGreen
endif

" --- vim-plug section vim plugin manager ------------------------------
" https://github.com/junegunn/vim-plug

" -- vim plugin options before loading -----------
let g:snipMate = { 'snippet_version' : 1 }
" Disable spell check toggle enable/disable with 'ZT' in normal mode
let g:enable_spelunker_vim = 0

" Specify a directory for plugins
" - Avoid using standard Vim directory names like 'plugin'
silent! if plug#begin('~/.vim/plugged')
" List all plugins here
" Shorthand notation, fetches https://github.com/{user}/{repo}

" * UI/UX plugins
Plug 'junegunn/fzf'
Plug 'junegunn/fzf.vim'
Plug 'jlanzarotta/bufexplorer'
Plug 'simeji/winresizer'
Plug 'tpope/vim-obsession'
Plug 'tpope/vim-unimpaired'
"Plug 'tpope/vim-repeat' " NOTE: enable for most frequently used plugins/cmds
Plug 'easymotion/vim-easymotion'
Plug 'junegunn/vim-peekaboo'
Plug 'preservim/nerdtree'
Plug 'milkypostman/vim-togglelist'
Plug 'pseewald/vim-anyfold'
Plug 'manid2/vim-ypclipboard'

" * Code editing plugins generic
Plug 'preservim/tagbar'
Plug 'tpope/vim-fugitive'
"Plug 'aperezdc/vim-template' " NOTE: enable when custom templates are created
" -- garbas/vim-snipmate dependencies begin ------
Plug 'MarcWeber/vim-addon-mw-utils'
Plug 'tomtom/tlib_vim'
Plug 'honza/vim-snippets'
" -- garbas/vim-snipmate dependencies end --------
Plug 'garbas/vim-snipmate'
Plug 'vim-scripts/a.vim'
"Plug 'vim-scripts/OmniCppComplete' " NOTE: hard to use
Plug 'vim-scripts/DoxygenToolkit.vim'
Plug 'bergercookie/vim-deb-preview'
Plug 'vim-scripts/gnupg'
Plug 'preservim/nerdcommenter'
Plug 'sukima/xmledit'
" TODO: modify tagsdb generation plugin to use custom command along with
" automatic tags updation
"Plug 'jsfaint/gen_tags.vim'
Plug 'preservim/vim-markdown'
Plug 'manid2/vim-cscope-settings'

" * Text editing plugins
Plug 'tpope/vim-surround'
Plug 'junegunn/vim-easy-align'
Plug 'google/vim-searchindex'
Plug 'kamykn/spelunker.vim'
"Plug 'dhruvasagar/vim-table-mode'

" * Syntax highlighters
Plug 'ekalinin/Dockerfile.vim'
Plug 'nathanalderson/yang.vim'

" List ends here. Plugins become visible to Vim after this call.
call plug#end()
endif

" --- statusline section -----------------------------------------------
set laststatus=2     " Always show statusline
function! s:statusline_expr()
	" Show vim-obsession status if available
	let session = "%{exists(':Obsession') ? ObsessionStatus() : ''}"
	let cmode=" [%{mode('0')}] "
	let bfno="[%n] "
	let relflnm="[%-0.32f] "
	let trunc="%<"
	let modflg = "%{&modified ? '[+] ' : !&modifiable ? '[x] ' : ''}"
	let ro  = "%{&readonly ? '[RO] ' : ''}"
	let ft  = "%{len(&filetype) ? '['.&filetype.'] ' : ''}"
	let ftfmt = "[%{&fileformat}] "
	let ften = "[%{&fileencoding ? &fileencoding : &encoding}] "
	let gitst = "%{exists('g:loaded_fugitive') ? fugitive#statusline() : ''}"
	let sep = ' %= '
	let pos = ' %-16(%l:%c%V [%L]%) '
	let higrp='%*'
	let pct = ' %P'
	hi StatusLine ctermbg=Green ctermfg=Black
	hi StatusLineNC ctermbg=LightGray ctermfg=Black
	return session.cmode.bfno.relflnm.trunc.modflg.ro.ft.
				\ftfmt.ften.gitst.sep.pos.higrp.pct
endfunction
let &statusline = s:statusline_expr()

" --- keymaps section --------------------------------------------------
" map leader key
"let mapleader="\<space>" " default '\'

" Save file in normal and also in insert mode
inoremap <C-S>               <C-O>:update<cr><esc>
nnoremap <C-S>               :update<cr>

" Quit
inoremap <C-Q>               <esc>:q<cr>
nnoremap <C-Q>               :q<cr>
nnoremap <Leader>q           :q<cr>
nnoremap <Leader>qa          :qa<cr>
nnoremap <Leader>fq          :q!<cr>
nnoremap <Leader>fqa         :qa!<cr>

" vim to shell, execute 'fg' command to bring vim back
nnoremap <Leader>sh          :shell<cr>
nnoremap <Leader>,sh         <C-Z>

" vim plugins keymaps
nnoremap <Leader>ntt         :NERDTreeToggle<cr>
nnoremap <Leader>tbt         :TagbarToggle<cr>

" fzf.vim keymaos
" refer: https://github.com/junegunn/fzf.vim#readme
nnoremap fff                 :Files<cr>
nnoremap fgf                 :GFiles<cr>
nnoremap ff'                 :Buffers<cr>
nnoremap ff,                 :Colors<cr>
nnoremap frg                 :Rg<cr>
nnoremap f,l                 :Lines<cr>
nnoremap ffl                 :BLines<cr>
nnoremap fft                 :Tags<cr>
nnoremap ffb                 :BTags<cr>
nnoremap ffm                 :Marks<cr>
nnoremap ffw                 :Windows<cr>
nnoremap ffvh                :History<cr>
nnoremap f;h                 :History:<cr>
nnoremap f's                 :History/<cr>
nnoremap fgc                 :Commits<cr>
nnoremap fbc                 :BCommits<cr>
nnoremap f,c                 :Commands<cr>
nnoremap f,m                 :Maps<cr>
nnoremap f't                 :Helptags<cr>
nnoremap f,f                 :Filetypes<cr>

command! -bang -nargs=* GGrep
			\ call fzf#vim#grep(
			\ 'git grep --line-number -- '.shellescape(<q-args>),
			\ 0,
			\ fzf#vim#with_preview({
			\   'dir': systemlist(
			\     'git rev-parse --show-toplevel')[0]}), <bang>0)

nnoremap f;g                 :GGrep<space>
nnoremap f'g                 :GGrep<cr>

" --- vim custom functions & keymaps -----------------------------------
" -- vim indent and format file
nnoremap <C-A>ff         gg=G``

" -- Toggle syntax highilight
function ToggleSyntax()
	if exists("g:syntax_on")
		syntax off
	else
		syntax enable
	endif
endfunction

command! -nargs=0 ToggleSyntax :call ToggleSyntax()

nnoremap <Leader>syn         :ToggleSyntax<cr>

" Vim grep ':vimgrep'
" TODO: Add grep within current files, buffers, current directory, recursive
nnoremap <Leader>vg          :vimgrep<space>

" -- vim sessions
let s:sessions_dir = '~/.vim/sessions'
exe 'nnoremap <Leader>ss :Obsession ' . s:sessions_dir . '/*.vim<C-D><BS><BS><BS><BS><BS>'
exe 'nnoremap <Leader>sr :so ' . s:sessions_dir. '/*.vim<C-D><BS><BS><BS><BS><BS>'
nnoremap <Leader>sp          :Obsession<CR>
